set(TSAR_DESCRIPTION "@TSAR_DESCRIPTION@")
set(TSAR_HOMEPAGE_URL "@TSAR_HOMEPAGE_URL@")

set(TSAR_LLVM_VERSION @LLVM_VERSION@)

# Set list of extenal tools, such as clang or llvm-link, available
# from TSAR distribution.
set(TSAR_LLVM_TOOLS @TSAR_LLVM_TOOLS@)

if (EXISTS ${CMAKE_CURRENT_LIST_DIR}/TSARExports.cmake)
  set(TSAR_EXPORT ON)
  include(${CMAKE_CURRENT_LIST_DIR}/TSARExports.cmake)
else()
  set(TSAR_EXPORT OFF)
  add_library(TSAR::tsar ALIAS tsar)
  if (TARGET tsar-server)
    add_library(TSAR::tsar-server ALIAS tsar-server)
  endif()
endif()

get_property(TSAR_CONFIGURATIONS TARGET TSAR::tsar PROPERTY IMPORTED_CONFIGURATIONS)
foreach(TC ${TSAR_CONFIGURATIONS})
  string(TOUPPER ${TC} TC_SUFFIX)
  get_property(TSAR_EXECUTABLE_${TC_SUFFIX}
    TARGET TSAR::tsar PROPERTY IMPORTED_LOCATION_${TC_SUFFIX})
  get_property(TSAR_SERVER_EXECUTABLE_${TC_SUFFIX}
    TARGET TSAR::tsar-server PROPERTY IMPORTED_LOCATION_${TC_SUFFIX})
  get_filename_component(TSAR_BINARY_DIR_${TC_SUFFIX} ${TSAR_EXECUTABLE_${TC_SUFFIX}} DIRECTORY)
  if ("@TSAR_INCLUDE_PATH@")
    get_filename_component(TSAR_INCLUDE_PATH_${TC_SUFFIX} ${TSAR_BINARY_DIR_${TC_SUFFIX}}/../@TSAR_INCLUDE_PATH@ ABSOLUTE)
  endif()
endforeach()

set(TSAR_EXECUTABLE)
set(TSAR_BINARY_DIR)
set(TSAR_INCLUDE_PATH)
set(TSAR_SERVER_EXECUTABLE)

list(LENGTH TSAR_CONFIGURATIONS TSAR_CONFIGURATIONS_LENGTH)
if (TSAR_CONFIGURATIONS_LENGTH EQUAL 1)
  list(GET TSAR_CONFIGURATIONS 0 TSAR_CONFIGURATION)
  string(TOUPPER ${TSAR_CONFIGURATION} TC_SUFFIX)
  get_property(TSAR_EXECUTABLE
    TARGET TSAR::tsar PROPERTY IMPORTED_LOCATION_${TC_SUFFIX})
  get_property(TSAR_SERVER_EXECUTABLE
    TARGET TSAR::tsar-server PROPERTY IMPORTED_LOCATION_${TC_SUFFIX})
  get_filename_component(TSAR_BINARY_DIR ${TSAR_EXECUTABLE_${TC_SUFFIX}} DIRECTORY)
  if ("@TSAR_INCLUDE_PATH@")
    get_filename_component(TSAR_INCLUDE_PATH ${TSAR_BINARY_DIR_${TC_SUFFIX}}/../@TSAR_INCLUDE_PATH@ ABSOLUTE)
  endif()
elseif (NOT TSAR_CONFIGURATIONS)
  get_property(TSAR_EXECUTABLE TARGET TSAR::tsar PROPERTY IMPORTED_LOCATION)
  get_property(TSAR_SERVER_EXECUTABLE TSAR::tsar-server PROPERTY IMPORTED_LOCATION)
  get_filename_component(TSAR_BINARY_DIR ${TSAR_EXECUTABLE} DIRECTORY)
  if ("@TSAR_INCLUDE_PATH@")
    get_filename_component(TSAR_INCLUDE_PATH ${TSAR_BINARY_DIR}/../@TSAR_INCLUDE_PATH@ ABSOLUTE)
  endif()
endif()

